plugins {
    id "java"
    id "org.springframework.boot" version "3.4.2"
    id "io.spring.dependency-management" version "1.1.7"
    id "jacoco"
    id "org.owasp.dependencycheck" version "11.1.1"
    id "com.diffplug.spotless" version "7.0.1"
}

ext {
    archUnitVersion = "1.3.0"
    jacocoVersion = "0.8.12"
    lombokVersion = "1.18.36"
    mockitoVersion = "5.15.2"
    swaggerVersion = "2.8.1"
}

group = "com.github.lomasz.llm"
version = "0.0.1-SNAPSHOT"

java {
    setSourceCompatibility(JavaVersion.VERSION_22)
    setTargetCompatibility(JavaVersion.VERSION_22)
}

repositories {
    mavenCentral()
    maven { url = "https://oss.jfrog.org/artifactory/oss-snapshot-local/" }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-data-rest"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$swaggerVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-starter-webflux"
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "io.projectreactor:reactor-test"
    testImplementation "com.tngtech.archunit:archunit-junit5:$archUnitVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
}

processResources {
    expand(project.properties)
}

clean {
    delete "out"
}

dependencyCheck {
    failBuildOnCVSS = 7
    format = "HTML"
    autoUpdate = true

    nvd {
        apiKey = System.getenv("NVD_API_KEY") ?: ""
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        //palantirJavaFormat()
        leadingTabsToSpaces(4)
    }
}

jacoco {
    toolVersion = "$jacocoVersion"
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    executionData(fileTree(layout.buildDirectory.get().asFile).include("jacoco/*.exec"))
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ["**/*Application*",
                              "**/config/**/*",
                              "**/exception/**/*",
                              "**/model/**/*"])
        }))
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    finalizedBy tasks.jacocoTestReport

    testLogging {
        events "passed", "skipped", "failed"
    }
}
